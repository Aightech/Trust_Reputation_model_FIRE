breed [ consumers consumer ]

consumers-own
[
  activity ;; alpha in the article
]

to init-consumer 
  ;; initialize properties of a consumer turtle
  set color blue
  set activity random-float (max-activity-consumers - min-activity-consumers) + min-activity-consumers 
  set rating-history n-values history-size [[0 0 0]]
  set history-index 0
  random-position
end

to  process-consumer
  ;; if the consumer ask for a service, find the supposed best provider and evaluate its performance
  if random-float 1.0 < activity ;; determine if the consumer ask for a service or not
  [
    ;; find the provider
    let nearest-providers min-n-of 10 providers [2 * asin((distance myself) / 2) * pi / 180]
    let nearest-providers-id [who] of nearest-providers
    let p 0 ; id of the provider we will ask a service
    ifelse trust-interaction-model or trust-witness-model or trust-certified-model 
    [ set p choose(nearest-providers-id) ] ; if trust model
    [ set p one-of nearest-providers-id ] ; if no trus model
    ;; calculate the performance of the provider
    let utility 0
    ask turtle p [ 
      set utility performance-provider(2 * asin((distance myself) / 2) * pi / 180)
      store-rating-provider p utility ticks
      set list-UG lput utility list-UG
    ]
    store-rating-consumer p utility ticks
  ]
end

to-report choose [ nearest-providers-id ]
  ;; return a the supposed best provider
  let T-max -1
  let id-max -1
  let res 0
  let T_k 0 ; trust value
  let rho_k 0 ; reliabitlity
  let Tfound 0
  let denominator_T 0
  let numerator_T 0  
  let T 0  ; global trust value
  let nb-T 0
  let T-moy 0
  let has-no-trust-value-list []
  
  foreach nearest-providers-id
  [ p ->
    set denominator_T 0
    set numerator_T 0
    
    ;; Interaction Trust
    if trust-interaction-model 
    [
      set res trust-interaction(p)
      set T_k item 0 res
      ifelse T_k = -2 [set T_k 0][set Tfound Tfound + 1]
      set rho_k item 1 res   
      set numerator_T numerator_T + wI * T_k * rho_k 
      set denominator_T denominator_T + ( wI * rho_k ) 
    ]
    
    ;; Witness Reputation
    if trust-witness-model 
    [
      set res trust-witness(p)
      set T_k item 0 res
      ifelse T_k = -2 [set T_k 0][set Tfound Tfound + 1]
      set rho_k item 1 res          
      set numerator_T numerator_T + wW * T_k * rho_k  
      set denominator_T denominator_T + ( wW * rho_k )   
    ]
    
    ;; Certified Reputation
    if trust-certified-model
    [
      set res trust-certified(p)
      set T_k item 0 res
      ifelse T_k = -2 [set T_k 0][set Tfound Tfound + 1]
      set rho_k item 1 res         
      set numerator_T numerator_T + wC * T_k * rho_k 
      set denominator_T denominator_T + ( wC * rho_k )
    ]    
    
    ifelse Tfound > 0 ;; if at least one value has been calculated
    [ 
      ifelse denominator_T != 0 
      [ set T numerator_T / denominator_T ][set T 0]
      set T-moy T-moy + T 
      set nb-T nb-T + 1
      if T >= T-max 
      [
        set T-max T ; save the current best trust value
        set id-max p ; save the id of the current best provider
      ]
    ]
    
    ;; No Trust
    [
      set has-no-trust-value-list lput p has-no-trust-value-list
    ]
  ]  
  
  let proba 0
  ifelse nb-T > 0
  [
    set T-moy T-moy / nb-T
    set proba exp(T-max / Temp) / (exp(T-max / Temp) + exp(T-moy / Temp))
    let rand random-float 1
    ifelse rand > proba and not empty? has-no-trust-value-list        
    ;; Exploration
    [ report one-of has-no-trust-value-list ] 
    ;; Exploitation
    [ report id-max ]
  ]
  ;; No Trust value calculated
  [report one-of has-no-trust-value-list ]
end

to-report trust-interaction [ p ]
  ;; for Interaction Trust model
  ;; return the trust value and reliability of an interaction between a consumer and provider p
  let list-of-review review-of(p)
  report compute-Tk(list-of-review)
end

to-report trust-witness [ p ]
  ;; for Witness Reputation model
  ;; return the trust value and reliability of an interaction between a consumer and provider p
  let list-of-review tree-consumers-review p nRL
  report compute-Tk(list-of-review)
end

to-report trust-certified [ p ]
  ;; for Certified Reputation model
  ;; return the trust value and reliability of an interaction between a consumer and provider p
  let list-of-review []
  ask turtle p [    
    set list-of-review rating-history
  ]
  report compute-Tk(list-of-review)
end

to-report tree-consumers-review [ p n ]
  ;; build the list of ratings of provider p from connected agents
  let consumers-acquaintances acquaintances with [breed = consumers] ;; extract consumers from acquaintances
  let n-cons min list nBF count consumers-acquaintances  
  let query-list min-n-of n-cons consumers-acquaintances [2 * asin((distance myself) / 2) * pi / 180] ;; extact nBF closest consumers
  let list-of-ratings [] ;; list with all the ratings of provider p
  let temp-list-of-ratings [] ;; temp list to save ratings of one specific consumer regarding a provider
  ask query-list
  [
    set temp-list-of-ratings review-of p    
    ifelse empty? temp-list-of-ratings AND n > 0
    [
      set list-of-ratings sentence tree-consumers-review p (n - 1) list-of-ratings
    ]
    [
      set list-of-ratings sentence temp-list-of-ratings list-of-ratings
    ]
  ]
  report list-of-ratings  
end

to-report compute-Tk [ list-of-rating ]
  ;; compute TK with the list L not empty and return TK
  let w 0
  let sum_w 0
  let Tk 0
  let denominator 0
  let rating-reliability 0
  let deviation-reliability 0
  ifelse empty? list-of-rating
  [ report (list -2 0) ]
  [
    ;print "list"
    ;print list-of-rating
    foreach list-of-rating
    [
      n -> 
      set w exp(-(ticks - item 2 n) / lambda)
      set Tk (Tk + w * (item 1 n))
      set sum_w (sum_w + w)
    ]
    set Tk Tk / sum_w
    ;print "rr"
    set rating-reliability (1 - exp( - gamma * sum_w))
    ;print rating-reliability
    foreach list-of-rating
    [
      n -> 
      set w exp(-(ticks - item 2 n) / lambda)
      set deviation-reliability deviation-reliability + w * (abs( item 1 n - Tk)) 
    ]
    ;print "dr"
    set deviation-reliability 1 - 0.5 * deviation-reliability / sum_w
    ;print deviation-reliability
    report (list Tk (rating-reliability * deviation-reliability))
  ]
end

to-report review-of [ b ]
  ;; if ask list of transaction with agent b 
  let list-of-rating []
  foreach rating-history
  [
    n -> 
    if empty? n != true and (item 0 n) = b
    [
        set list-of-rating lput n list-of-rating
    ]
  ]
  report list-of-rating
end

to store-rating-consumer [ id v t ]
  ;;at the end of a deal
  set rating-history replace-item history-index rating-history [0 0 0];; init the rating slot.
  set rating-history (replace-item history-index rating-history
                  (replace-item 0 (item history-index rating-history) id));;set the id of the selected provider
  set rating-history (replace-item history-index rating-history
                  (replace-item 1 (item history-index rating-history) (v / 10)));;set the UG of the deal
  set rating-history (replace-item history-index rating-history
                  (replace-item 2 (item history-index rating-history) t));;set the time of the deal

  ifelse history-index < history-size - 1 [ set history-index history-index + 1 ] [ set history-index 0 ] ;;inc the index of the history of rating
end